apply plugin: 'application'

sourceSets {
    main {
        java {
            srcDir 'src'
        }

        resources {
            srcDirs = ['src/GUI', 'src/icons']
        }
    }

    test {
        java {
            srcDir 'test'
        }
    }
}

repositories {
    jcenter()

    repositories {
        flatDir {
            // Copy triplecheck-engine.jar to here.
            dirs 'lib'
        }
    }
}

dependencies {
    compile 'org.apache.commons:commons-compress:1.8.1'
    compile 'org.mapdb:mapdb:1.0.1'
    compile 'net.htmlparser.jericho:jericho-html:3.3'

    compile 'com.github.triplecheck:triplecheck-engine:0.0.0'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all'
    options.encoding = 'UTF-8'
}

mainClassName = 'main.start'

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }

    // Build a "Fat Jar" containing all runtime dependencies, excluding unwanted files.
    from(configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "LICENSE.txt"
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}
